plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'blog'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '21'

archivesBaseName = 'vans_story_be'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // === 스프링 부트 핵심 스타터 ===
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    
    // === 보안 관련 ===
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    
    // === JSON 처리 ===
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    
    // === 데이터베이스 ===
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    runtimeOnly 'com.h2database:h2'
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    
    //=== Validation ===
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // === 개발 도구 ===
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    
    // === 로깅 ===
    implementation 'ch.qos.logback:logback-classic'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    
    // === 코드 생성 도구 ===
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    
    // === 테스트 ===
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // === 인증 ===
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    
    // === 캐싱 ===
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('copyJavadoc', Copy) {
    from "${buildDir}/docs/javadoc"
    into "${projectDir}/docs/javadoc"
    dependsOn javadoc
}

tasks.javadoc {
    options.encoding = 'UTF-8'
    options.memberLevel = JavadocMemberLevel.PRIVATE
    
    classpath += configurations.compileClasspath
    
    options.addStringOption('Xdoclint:none', '-quiet')
    
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
        options.addBooleanOption('html5', true)
    }
    
    // Javadoc 출력 디렉토리 설정
    destinationDir = file("${projectDir}/docs/javadoc")
}
